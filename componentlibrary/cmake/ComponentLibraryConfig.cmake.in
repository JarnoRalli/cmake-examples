###########
#
# This is the configuration file for the ComponentLibrary.
#
# VARIABLES
#
# Following variables are set by the configuration file:
#
# ComponentLibrary_INCLUDE_DIR - Path to the /include directory
# ComponentLibrary_LIB_DIR - Path to the /lib directory
# ComponentLibrary_FOUND - set to FALSE if a requested component doesn't exist
#
# COMPONENTS
#
# This library supports the following components (targets):
#   @MAIN_LIBRARY_LIST@
#
# In the consumer cmake script, you can write:
#   find_package(ComponentLibrary REQUIRED COMPONENTS <name-of-the-component>)
#   target_link_library(target ComponentLibrary::<name-of-the-component>)
#
# If you don't define any components, then you can link against the following target:
#   ComponentLibrary::ComponentLibrary
# , which contains all of the targets.
#
###########

include(CMakeFindDependencyMacro)

@PACKAGE_INIT@

# Check if the include- and lib directories exist
set_and_check(ComponentLibrary_INCLUDE_DIR "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@")
set_and_check(ComponentLibrary_LIB_DIR "@PACKAGE_CMAKE_INSTALL_LIBDIR@")

####################
# Handle components
####################

# This is not needed as components are handled below
#check_required_components(@MAIN_LIBRARY_LIST@)

# A list of components that the ComponentLibrary has
set(_supported_components @MAIN_LIBRARY_LIST@)

# Handle components. If no components have been specified in the find_package then all of the components are loaded.
if(NOT ComponentLibrary_FIND_COMPONENTS) # no components have been defined, therefore load all of the components
    foreach(_comp ${_supported_components})
        include("${CMAKE_CURRENT_LIST_DIR}/ComponentLibrary${_comp}Targets.cmake")
    endforeach()
    # Load the meta-component
    include("${CMAKE_CURRENT_LIST_DIR}/ComponentLibraryTargets.cmake")
else() # load the given set of components
    foreach(_comp ${ComponentLibrary_FIND_COMPONENTS})
        if (NOT ";${_supported_components};" MATCHES ${_comp})
            set(ComponentLibrary_FOUND False)
            set(ComponentLibrary_NOT_FOUND_MESSAGE "ComponentLibrary does not have the following component: ${_comp}")
        endif()
        include("${CMAKE_CURRENT_LIST_DIR}/ComponentLibrary${_comp}Targets.cmake")
    endforeach()
endif()
